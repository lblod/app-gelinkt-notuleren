DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?o .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s a ?type ; ?p ?o .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/MandatarisStatusCode>,
            <http://data.vlaanderen.be/ns/mandaat#Mandataris>,
            <http://data.vlaanderen.be/ns/mandaat#Mandaat>
            ))
    }
}
;
INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?o .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph> {
        ?s a ?type ; ?p ?o .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/MandatarisStatusCode>,
            <http://data.vlaanderen.be/ns/mandaat#Mandataris>,
            <http://data.vlaanderen.be/ns/mandaat#Mandaat>
            ))
    }
}
;
# here we want to only replace data or insert new data, never remove old data.

# First delete triples that exist in the public graph and will be inserted again from temp-ingest-graph 
# The difference here is that data not being overwritten bij temp-ingest-graph will stay in the public graph
DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?old .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph> {
        ?s a ?type ; ?p ?new .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/BeleidsdomeinCode>,
            <http://www.w3.org/ns/org#Membership>,
            <http://data.vlaanderen.be/ns/mandaat#Fractie>,
            <http://mu.semte.ch/vocabularies/ext/BestuursfunctieCode>
            ))
    }
    GRAPH <http://mu.semte.ch/graphs/public> {
      ?s ?p ?old .
    }
}
;
# Secondly, insert all related data to the public graph.
# this will insert triples removed from the public graph in the above query 
# and triples that are "new" (were not present in public graph before)
INSERT{
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?new .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph> {
        ?s a ?type ; ?p ?new .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/BeleidsdomeinCode>,
            <http://www.w3.org/ns/org#Membership>,
            <http://data.vlaanderen.be/ns/mandaat#Fractie>,
            <http://mu.semte.ch/vocabularies/ext/BestuursfunctieCode>
            ))
    }
}
;
