PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX prov: <http://www.w3.org/ns/prov#>
DELETE {
    GRAPH <http://mu.semte.ch/graphs/reports> {
        ?s2  a <http://lblod.data.gift/vocabularies/reporting/Report>;
            dct:title ?title;
            dct:created ?created;
            dct:description ?description;
            mu:uuid ?uuid;
            prov:generated ?file.
        ?file a nfo:FileDataObject;
            dct:created ?fileCreated;
            dbpedia:fileExtension ?fileExtension;
            dct:format ?fileFormat;
            mu:uuid ?fileUuid;
            nfo:fileName ?fileName;
            nfo:fileSize ?fileSize.
        ?physicalFile  a nfo:FileDataObject;
            nie:dataSource ?file;
            dct:created ?physicalFileCreated;
            dbpedia:fileExtension ?physicalFileExtension;
            dct:format ?physicalFileFormat;
            mu:uuid ?physicalFileUuid;
            nfo:fileName ?physicalFileName;
            nfo:fileSize ?physicalFileSize.
    }
}WHERE {
    GRAPH <http://mu.semte.ch/graphs/reports> {
        ?s1 dct:title ?title.
        ?s2 dct:title ?title;
            dct:created ?created;
            dct:description ?description;
            mu:uuid ?uuid;
            prov:generated ?file.
        ?file a nfo:FileDataObject;
            dct:created ?fileCreated;
            dbpedia:fileExtension ?fileExtension;
            dct:format ?fileFormat;
            mu:uuid ?fileUuid;
            nfo:fileName ?fileName;
            nfo:fileSize ?fileSize.
        ?physicalFile nie:dataSource ?file;
            dct:created ?physicalFileCreated;
            dbpedia:fileExtension ?physicalFileExtension;
            dct:format ?physicalFileFormat;
            mu:uuid ?physicalFileUuid;
            nfo:fileName ?physicalFileName;
            nfo:fileSize ?physicalFileSize.
        ?s1 a <http://lblod.data.gift/vocabularies/reporting/Report>.
        ?s2 a <http://lblod.data.gift/vocabularies/reporting/Report>.
    }
    FILTER(STR(?s1) < STR(?s2))
}