PREFIX nmo: <http://oscaf.sourceforge.net/nmo.html#>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX validation: <http://mu.semte.ch/vocabularies/validation/>
PREFIX bbcdr: <http://mu.semte.ch/vocabularies/ext/bbcdr/>
PREFIX export: <http://mu.semte.ch/vocabularies/ext/export/>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX schema: <http://schema.org/>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX pav: <http://purl.org/pav/>
PREFIX nao: <http://www.semanticdesktop.org/ontologies/2007/08/15/nao#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX regorg: <https://www.w3.org/ns/regorg#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dul: <http://www.ontologydesignpatterns.org/ont/dul/DUL.owl#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX generiek: <http://data.vlaanderen.be/ns/generiek#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX tmp: <http://mu.semte.ch/vocabularies/tmp/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX rm: <http://mu.semte.ch/vocabularies/logical-delete/>
PREFIX typedLiterals: <http://mu.semte.ch/vocabularies/typed-literals/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX app: <http://mu.semte.ch/app/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

WITH <http://mu.semte.ch/application>

# insert burgemeetser
DELETE {
  ?existingOrgaanInTijd org:hasPost ?mandaat .
}

INSERT {
  ?bestuursorgaan a besluit:Bestuursorgaan .
  ?bestuursorgaan mu:uuid ?uuidOrgaan .
  ?bestuursorgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/4955bd72cd0e4eb895fdbfab08da0284> .
  ?bestuursorgaan besluit:bestuurt ?bestuurseenheid .
  ?bestuursorgaan skos:prefLabel ?bestuursorgaanNaam .

  ?bestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan .

  ?bestuursorgaanInTijd a besluit:Bestuursorgaan .
  ?bestuursorgaanInTijd mu:uuid ?uuidInTijd .
  ?bestuursorgaanInTijd mandaat:bindingStart "2012-10-14"^^<http://www.w3.org/2001/XMLSchema#date> .
  ?bestuursorgaanInTijd org:hasPost ?mandaat .
}

WHERE {
  ?bestuursfunctie mu:uuid "5ab0e9b8a3b2ca7c5e000013" .
  ?mandaat org:role ?bestuursfunctie .
  ?mandaat ^org:hasPost ?existingOrgaanInTijd .
  ?existingOrgaanInTijd mandaat:isTijdspecialisatieVan ?orgaan .
  ?orgaan besluit:bestuurt ?bestuurseenheid .
  ?bestuurseenheid skos:prefLabel ?eenheidNaam .

  BIND(CONCAT(STR(?eenheidNaam), " ", "Burgemeester") AS ?bestuursorgaanNaam) .
  BIND(SHA256(CONCAT("1aee45a1-0bb6-49a0-976f-461c84e269f2", ":", STR(?bestuursorgaanNaam))) AS ?uuidOrgaan) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidOrgaan))) AS ?bestuursorgaan) .
  BIND(SHA256(CONCAT("a9d31fe8-4e40-4815-acdf-be17927d0bb9", ":", STR(?bestuursorgaanNaam))) AS ?uuidInTijd) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", ?uuidInTijd)) AS ?bestuursorgaanInTijd) .
};

# insert college
DELETE {
  ?existingOrgaanInTijd org:hasPost ?mandaat .
}

INSERT {
  ?bestuursorgaan a besluit:Bestuursorgaan .
  ?bestuursorgaan mu:uuid ?uuidOrgaan .
  ?bestuursorgaan besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000006> .
  ?bestuursorgaan besluit:bestuurt ?bestuurseenheid .
  ?bestuursorgaan skos:prefLabel ?bestuursorgaanNaam .

  ?bestuursorgaanInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaan .

  ?bestuursorgaanInTijd a besluit:Bestuursorgaan .
  ?bestuursorgaanInTijd mu:uuid ?uuidInTijd .
  ?bestuursorgaanInTijd mandaat:bindingStart "2012-10-14"^^<http://www.w3.org/2001/XMLSchema#date> .
  ?bestuursorgaanInTijd org:hasPost ?mandaat .
}

# select schepenen
WHERE {
  ?bestuursfunctie mu:uuid "5ab0e9b8a3b2ca7c5e000014" .
  ?mandaat org:role ?bestuursfunctie .
  ?mandaat ^org:hasPost ?existingOrgaanInTijd .
  ?existingOrgaanInTijd mandaat:isTijdspecialisatieVan ?orgaan .
  ?orgaan besluit:bestuurt ?bestuurseenheid .
  ?bestuurseenheid skos:prefLabel ?eenheidNaam .

  BIND(CONCAT(STR(?eenheidNaam), " ", "College Van burgemeester en schepenen") AS ?bestuursorgaanNaam) .
  BIND(SHA256(CONCAT("1aee45a1-0bb6-49a0-976f-461c84e269f2", ":", STR(?bestuursorgaanNaam))) AS ?uuidOrgaan) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidOrgaan))) AS ?bestuursorgaan) .
  BIND(SHA256(CONCAT("a9d31fe8-4e40-4815-acdf-be17927d0bb9", ":", STR(?bestuursorgaanNaam))) AS ?uuidInTijd) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", ?uuidInTijd)) AS ?bestuursorgaanInTijd) .
};

# burgemeester
INSERT {
  ?collegeInTijd org:hasPost ?burgemeester
}

WHERE {
  ?orgaanCollege besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000006> .
  ?orgaanBurgemeester besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/4955bd72cd0e4eb895fdbfab08da0284> .
  ?orgaanCollege besluit:bestuurt ?bestuurseenheid .
  ?orgaanBurgemeester besluit:bestuurt ?bestuurseenheid .

  ?collegeInTijd mandaat:isTijdspecialisatieVan ?orgaanCollege .
  ?burgemeesterInTijd mandaat:isTijdspecialisatieVan ?orgaanBurgemeester .

  ?burgemeesterInTijd org:hasPost ?burgemeester .
  ?burgemeester org:role ?burgemeesterRol .
  ?burgemeesterRol mu:uuid "5ab0e9b8a3b2ca7c5e000013" .
};