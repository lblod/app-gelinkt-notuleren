PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX bbcdr: <http://mu.semte.ch/vocabularies/ext/bbcdr/>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX schema: <http://schema.org/>
PREFIX person: <http://www.w3.org/ns/person#>

INSERT {
  GRAPH ?g {
    ?lidmaatschap ?lidmaatschapP ?lidmaatschapO .
  }
} WHERE {
  GRAPH ?g {
    FILTER(STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/organization/")) 

    ?mandataris a mandaat:Mandataris .
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?mandataris org:hasMembership ?lidmaatschap .
    ?lidmaatschap ?lidmaatschapP ?lidmaatschapO .
  }
}

;

INSERT {
  GRAPH ?g {
    ?fractie ?fractieP ?fractieO .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
                     mu:uuid ?groupId .

    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?groupId)) as ?g)

    ?fractie org:linkedTo ?bestuurseenheid .
    ?fractie ?fractieP ?fractieO .
  }
}

;

INSERT {
  GRAPH ?g {
    ?tijdsinterval ?tijdsintervalP ?tijdsintervalO .
  }
} WHERE {
  GRAPH ?g {
    FILTER(STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/organization/")) 

    ?lidmaatschap a org:Membership .
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?lidmaatschap org:memberDuring ?tijdsinterval .
    ?tijdsinterval ?tijdsintervalP ?tijdsintervalO .
  }
}
