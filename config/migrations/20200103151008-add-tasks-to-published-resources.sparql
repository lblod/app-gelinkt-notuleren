PREFIX sign: <http://mu.semte.ch/vocabularies/ext/signing/>
PREFIX nuao: <http://www.semanticdesktop.org/ontologies/2010/01/25/nuao#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX dct: <http://purl.org/dc/terms/>


INSERT {
  GRAPH ?graph {
    ?task a task:Task;
      mu:uuid ?taskUuid;
      adms:status <http://lblod.data.gift/besluit-publicatie-melding-statuses/success>;
      task:numberOfRetries 0;
      dct:created ?date;
      dct:modified ?date;
      dct:creator <http://lblod.data.gift/services/besluit-publicatie-melding-service/manual/migration>;
      nuao:involves ?resource.
  }
} WHERE
  {
   GRAPH ?graph {
     ?resource a sign:PublishedResource.
     ?resource <http://mu.semte.ch/vocabularies/ext/besluit-publicatie-publish-service/status> <http://mu.semte.ch/vocabularies/ext/besluit-publicatie-publish-service/status/success>.

    BIND(SHA256(CONCAT("a6bd53a9-3ec7-4e42-88e4-b01d05ebca96", ":2020-01-03:manual-task-melding-publicatie", STR(?resource))) AS ?taskUuid) .
    BIND(IRI(CONCAT("http://lblod.data.gift/besluit-publicatie-melding-events/", STR(?taskUuid))) AS ?task) .
    BIND("1970-01-01T00:00:01+00:00" as ?date).

     FILTER NOT EXISTS {
      ?task nuao:involves ?resource.
     }
  }
}