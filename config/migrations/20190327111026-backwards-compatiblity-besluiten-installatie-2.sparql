#################################################
# MIGRATES DATA FROM INSTALLATIEVERGADERING TO
# RE-UPDATED MODEL
# From the published data agenda,
#  besluitenlijsten and uittreksels are created
#################################################
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX nie:     <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav:  <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX dct: <http://purl.org/dc/terms/>

# insert agenda (only 1 agenda)
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {

    ?agendaUri a ext:Agenda.
    ?agendaUri mu:uuid ?agendaUuid.
    ?agendaUri ext:agendaAgendapunt ?agendapunt.

    ?zitting ext:agenda ?agendaUri.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?zitting a besluit:Zitting.
    ?zitting besluit:behandelt ?agendapunt.
    BIND(SHA256(CONCAT(STR(?zitting), "5a51fa35-6047-4db3-89ce-2aaac9006695", "agenda")) as ?agendaUuid) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/agendas/", ?agendaUuid)) AS ?agendaUri) .
 }
}

;

# insert besluitenlijsten
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?zitting ext:besluitPublicatieLinkedBesluit ?besluit.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {

    ?besluitenlijstUri a ext:Besluitenlijst.
    ?besluitenlijstUri mu:uuid ?besluitenlijstUuid.
    ?besluitenlijstUri ext:besluitenlijstBesluit ?besluit.

    ?zitting ext:besluitenlijst ?besluitenlijstUri.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?zitting a besluit:Zitting.
    ?zitting ext:besluitPublicatieLinkedBesluit ?besluit.
    BIND(SHA256(CONCAT(STR(?zitting), "cda29048-e630-4b74-aa79-1f2f48ef1cee", "besluitenlijst")) as ?besluitenlijstUuid) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/besluitenlijsten/", ?besluitenlijstUuid)) AS ?besluitenlijstUri) .
 }
}

;

# insert uittreksel
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?zitting ext:besluitPublicatieLinkedBvap ?bvap.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {

    ?uittrekselUri a ext:Uittreksel.
    ?uittrekselUri mu:uuid ?uittrekselUuid.
    ?uittrekselUri ext:uittrekselBvap ?bvap.

    ?zitting ext:uittreksel ?uittrekselUri.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?zitting a besluit:Zitting.
    ?zitting ext:besluitPublicatieLinkedBvap ?bvap.
    BIND(SHA256(CONCAT(STR(?zitting), "1cf441d7-fd3c-45f2-9f7c-a870e48f4908", "uittreksel")) as ?uittrekselUuid) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/uittreksels/", ?uittrekselUuid)) AS ?uittrekselUri) .
 }
}

;